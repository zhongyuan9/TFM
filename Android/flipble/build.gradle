apply plugin: 'com.android.library'
//Gradle Bintray Plugin
apply plugin: 'com.jfrog.bintray'
//Gradle Android Maven plugin
apply plugin: 'com.github.dcendents.android-maven'

android {
    compileSdkVersion build_versions.target_sdk
    buildToolsVersion build_versions.build_tools

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 37
        versionName version

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "com.android.support:support-annotations:$versions.support"

    // Dependencies for local unit tests
    testImplementation "junit:junit:$versions.junit"
    testImplementation "org.mockito:mockito-core:$versions.mockito"
    testImplementation "org.hamcrest:hamcrest-all:$versions.hamcrest"
    testImplementation "org.powermock:powermock-module-junit4:$versions.powerMockito"
    testImplementation "org.powermock:powermock-api-mockito:$versions.powerMockito"

    // Android Testing Support Library's runner and rules
    androidTestImplementation "com.android.support.test:runner:$versions.runner"
    androidTestImplementation "com.android.support.test:rules:$versions.rules"

    // Espresso UI Testing dependencies.
    androidTestImplementation "com.android.support.test.espresso:espresso-core:$versions.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-contrib:$versions.espresso"
    androidTestImplementation "com.android.support.test.espresso:espresso-intents:$versions.espresso"

}

version = "0.6.1"
group = "cn.bingerz.android"
def siteUrl = 'https://github.com/bingerz/flip-ble'
def gitUrl = 'https://github.com/bingerz/flip-ble.git'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                name 'FlipBLE'
                packaging 'aar'
                description 'A stable, easy-to-use framework developed based on the Android Bluetooth Low Energy API.'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'bingerz'
                        name 'bingerz'
                        email 'hanbing0604@gmail.com'
                    }
                }

                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}


task javadoc(type: Javadoc) {
    options.encoding = "UTF-8"
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}


task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

javadoc {
    options {
        encoding "UTF-8"
        charSet 'UTF-8'
        author true
        version true
        links "http://docs.oracle.com/javase/7/docs/api"
        title 'A Library of FlipBLE'   // 文档标题
    }
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        userOrg = "bingerz"
        repo = "maven"
        name = "flip-ble"
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}